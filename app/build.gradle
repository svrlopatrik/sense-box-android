apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    flavorDimensions "default"
    defaultConfig {
        //version information
        def versionMajor    = 1
        def versionMinor    = 0
        def versionPatch    = 0

        applicationId "sk.kotlin.sensebox"
        minSdkVersion 18
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }
    buildTypes {
        debug {
            debuggable true
            useProguard false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }
}

ext {
    appcompat                       = '27.1.1'
    constraint_layout               = '1.1.3'
    dagger                          = '2.14.1'
    viewmodel_livedata              = '1.1.1'
    room                            = '1.1.1'
    paging                          = '1.0.1'
    timber                          = '4.6.1'
    rx_java                         = '2.1.8'
    rx_android                      = '2.0.2'
    rx_room                         = '1.1.1'
    db_debug                        = '1.0.0'
    easy_permissions                = '1.3.0'
    mp_android_chart                = '3.0.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Kotlin
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //Android support
    implementation "com.android.support:appcompat-v7:$appcompat"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout"
    implementation "com.android.support:recyclerview-v7:$appcompat"
    implementation "com.android.support:cardview-v7:$appcompat"
    implementation "com.android.support:design:$appcompat"

    //Dagger
    implementation "com.google.dagger:dagger:$dagger"
    implementation "com.google.dagger:dagger-android:$dagger"
    implementation "com.google.dagger:dagger-android-support:$dagger"
    kapt "com.google.dagger:dagger-compiler:$dagger"
    kapt "com.google.dagger:dagger-android-processor:$dagger"

    //Architecture components
    implementation "android.arch.lifecycle:extensions:$viewmodel_livedata"
    implementation "android.arch.persistence.room:runtime:$room"
    annotationProcessor "android.arch.persistence.room:compiler:$room"
    kapt "android.arch.persistence.room:compiler:$room"
    implementation "android.arch.paging:runtime:$paging"

    //Timber
    implementation "com.jakewharton.timber:timber:$timber"

    //RxJava
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android"
    implementation "io.reactivex.rxjava2:rxjava:$rx_java"
    implementation "android.arch.persistence.room:rxjava2:$rx_room"

    //Db debug
    //wifi -> http://YOUR_PHONE_IP_ADDRESS:8080
    //usb -> 'adb forward tcp:8080 tcp:8080' then http://localhost:8080/
    debugImplementation "com.amitshekhar.android:debug-db:$db_debug"

    //Easy permissions
    implementation "pub.devrel:easypermissions:$easy_permissions"

    //Charts
    implementation "com.github.PhilJay:MPAndroidChart:v$mp_android_chart"
}
